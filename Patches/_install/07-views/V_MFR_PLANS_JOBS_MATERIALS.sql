IF OBJECT_ID('V_MFR_PLANS_JOBS_MATERIALS') IS NOT NULL DROP VIEW V_MFR_PLANS_JOBS_MATERIALS
GO
-- SELECT TOP 100 * FROM V_MFR_PLANS_JOBS_MATERIALS
CREATE VIEW V_MFR_PLANS_JOBS_MATERIALS
AS
SELECT
	X.PLAN_JOB_ID,
	X.ITEM_ID,
	ITEM_NAME = P.NAME,
	X.MATERIAL_ID,
	MATERIAL_NAME = P2.NAME,
	X.Q_BRUTTO,
	X.Q_BRUTTO_PRODUCT,
	X.UNIT_NAME
FROM (
	SELECT
		JD.PLAN_JOB_ID,
		JD.ITEM_ID, 
		MATERIAL_ID = DI.ITEM_ID,
		DI.UNIT_NAME,
		Q_BRUTTO = SUM(DI.Q_BRUTTO),
		Q_BRUTTO_PRODUCT = SUM(JD.PLAN_Q * DI.Q_BRUTTO)
	FROM MFR_PLANS_JOBS_DETAILS JD
		JOIN SDOCS_MFR_CONTENTS C ON C.CONTENT_ID = JD.CONTENT_ID
			JOIN SDOCS_MFR_DRAFTS D ON D.DRAFT_ID = C.DRAFT_ID
				JOIN SDOCS_MFR_DRAFTS_ITEMS DI ON DI.DRAFT_ID = D.DRAFT_ID
	WHERE DI.IS_BUY = 1
	GROUP BY
		JD.PLAN_JOB_ID, JD.ITEM_ID, DI.ITEM_ID, DI.UNIT_NAME
	) X
	JOIN PRODUCTS P ON P.PRODUCT_ID = X.ITEM_ID
	JOIN PRODUCTS P2 ON P2.PRODUCT_ID = X.MATERIAL_ID
WHERE X.Q_BRUTTO_PRODUCT > 0
	AND (
		NOT EXISTS(SELECT 1 FROM APP_REGISTRY WHERE REGISTRY_ID = 'MFRPRINTMATERIALSATTR')
		OR EXISTS(
			SELECT 1
			FROM PRODUCTS_ATTRS PA
				JOIN MFR_ATTRS A ON A.NAME = (SELECT TOP 1 VAL_STRING FROM APP_REGISTRY WHERE REGISTRY_ID = 'MFRPRINTMATERIALSATTR')
			WHERE PA.ATTR_VALUE = 'ОСНОВНОЙ'
				AND PA.PRODUCT_ID = X.MATERIAL_ID
			)
		)
GO
